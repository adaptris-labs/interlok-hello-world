ext {
  adpCoreVersion = project.hasProperty('adpCoreVersion') ? project.getProperty('adpCoreVersion') : '3.6-SNAPSHOT'
}
apply plugin: 'java'

configurations {
  antJunit
}

repositories {
  mavenCentral()
  maven { url "https://development.adaptris.net/nexus/content/groups/public" }
  maven { url "https://development.adaptris.net/nexus/content/repositories/snapshots" }
  maven { url "https://development.adaptris.net/nexus/content/repositories/releases" }
}

dependencies {
  compile ("com.adaptris:interlok-service-tester:$adpCoreVersion") { changing= true}
  antJunit ("org.apache.ant:ant-junit:1.8.2")
}

sourceSets {
  test {
    resources {
      srcDir "$projectDir/test/resources"
    }
  }
}

check.dependsOn -= test
task test(overwrite: true) {
  doLast {
    tasks.interlokTest.execute()
    antJunitReport()
  }
}
check.dependsOn += test
test.dependsOn += processTestResources

task(interlokTest, type: JavaExec) {
  main = 'com.adaptris.tester.runners.TestExecutor'
  classpath = sourceSets.test.runtimeClasspath
  args "$projectDir/test/src/test.xml"
  args "$buildDir/test-results/" 
}

def antJunitReport() {
  ant.taskdef(
    name: 'junitreport',
    classname: 'org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator',
    classpath: configurations.antJunit.asPath
  )

  ant.junitreport(todir: "$buildDir/test-results") {
    fileset(dir: "$buildDir/test-results", includes: 'TEST-*.xml')
    report(todir: "$buildDir/test-results/html", format: "frames" )
  }
}
